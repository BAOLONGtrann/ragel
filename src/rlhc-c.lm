
namespace c_out
	token _IN_ /''/
	token _EX_ /''/

	lex
		token comment /
			'//' any* :> '\n' |
			'/*' any* :>> '*/'
		/

		token id
			/[a-zA-Z_][a-zA-Z_0-9]*/

		token number /
			[0-9]+
		/

		token symbol /
			'!' | '#' | '$' | '%' | '&' | '(' | ')' | '*' |
			'+' | ',' | '-' | '.' | '/' | ':' | ';' | '<' |
			'=' | '>' | '?' | '@' | '[' | ']' | '^' | '|' |
			'~' /

		literal `{ `}

		token string /
				'"' ( [^"\\] | '\\' any ) * '"' |
				"'" ( [^'\\] | '\\' any ) * "'"
			/

		ignore
			/[ \t\v\r\n]+/
	end

	def item
		[comment]
	|	[id]
	|	[number]
	|	[symbol]
	|	[string]
	|	[`{ _IN_ item* _EX_ `} ]

	def c_out
		[_IN_ _EX_ item*]
end

global CParser: parser<c_out::c_out>

void c_tok_list( TL: host::tok* )
{
	for Tok: host::tok in repeat(TL) {
		if match Tok [host::`${ StmtList: stmt* host::`}$]
			send CParser "{[c_stmt_list( StmtList )]}"
		elsif match Tok [host::`={ Expr: expr host::`}=]
			send CParser "([c_expr( Expr )])"
		else {
			send CParser [Tok]
		}
	}
}

void c_embedded_host( EmbeddedHost: embedded_host )
{
	switch EmbeddedHost
	case [`host `( string `, uint `) `={ TL: host::tok* host::`}=]
	{
		send CParser
			"([c_tok_list( TL )])"
	}
	case [`host `( string `, uint `) `${ TL: host::tok* host::`}$]
	{
		send CParser 
			"{[c_tok_list( TL )]}
	}
	case [`host `( string `, uint `) `@{ TL: host::tok* host::`}@]
	{
		send CParser
			[c_tok_list( TL )]
	}
}

void c_expr_factor( ExprFactor: expr_factor )
{
	if match ExprFactor
		[EH: embedded_host]
	{
		send CParser
			[c_embedded_host( EH )]
	}
	elsif match ExprFactor
		[O:`( TL: expr C: `)]
	{
		send CParser
			[O c_expr(TL) C]
	}
	elsif match ExprFactor
		[ident O: `[ TL: expr C: `]]
	{
		send CParser
			[ExprFactor.ident O c_expr( TL ) C]
	}
	elsif match ExprFactor
		['offset' '(' ident ',' expr ')']
	{
		send CParser
			[ExprFactor.ident ' + ' c_expr( ExprFactor.expr )]
	}
	elsif match ExprFactor
		['deref' '(' ident ',' expr ')']
	{
		send CParser
			"(*( [c_expr(ExprFactor.expr)] ))
	}
	elsif match ExprFactor
			[T: `TRUE]
	{
		T.data = '1'
		send CParser [T]
	}
	elsif match ExprFactor
			[F: `FALSE]
	{
		F.data = '0'
		send CParser [F]
	}
	elsif match ExprFactor
			[N: `nil]
	{
		N.data = '0'
		send CParser [N]
	}
	elsif match ExprFactor
			[Number: number]
	{
		c_number( Number )
	}
	elsif match ExprFactor [E1: embedded_host `-> E2: expr_factor]
	{
		# The accessor operator is contained wihtin the lhs.
		c_embedded_host( E1 )
		c_expr_factor( E2 )
	}
	elsif match ExprFactor [Open: `( Type: type Close: `) expr_factor]
	{
		send CParser [Open]
		c_type( Type )
		send CParser [Close]
		c_expr_factor( ExprFactor._expr_factor )
	}
	else {
		# Catches cases not specified
		send CParser [ExprFactor]
	}
}

void c_lvalue( ExprFactor: lvalue )
{
	if match ExprFactor
		[EH: embedded_host]
	{
		send CParser
			[c_embedded_host( EH )]
	}
	elsif match ExprFactor
		[ident O: `[ TL: expr C: `]]
	{
		send CParser
			[ExprFactor.ident O c_expr( TL ) C]
	}
	elsif match ExprFactor [E1: embedded_host `-> E2: lvalue]
	{
		# The accessor operator is contained wihtin the lhs.
		c_embedded_host( E1 )
		c_lvalue( E2 )
	}
	else {
		# Catches cases not specified
		send CParser [ExprFactor]
	}
}

void c_expr_factor_op( ExprFactorOp: expr_factor_op )
{
	if match ExprFactorOp [B: `! expr_factor_op]
	{
		send CParser [B]
		c_expr_factor_op( ExprFactorOp._expr_factor_op )
	}
	elsif match ExprFactorOp [T: `~ expr_factor_op]
	{
		send CParser [T]
		c_expr_factor_op( ExprFactorOp._expr_factor_op )
	}
	elsif match ExprFactorOp [expr_factor]
	{
		c_expr_factor( ExprFactorOp.expr_factor )
	}
}

void c_expr_bitwise( ExprBitwise: expr_bitwise )
{
	if match ExprBitwise [expr_bitwise A: `& expr_factor_op]
	{
		c_expr_bitwise( ExprBitwise._expr_bitwise )
		send CParser [A]
		c_expr_factor_op( ExprBitwise.expr_factor_op )
	}
	elsif match ExprBitwise [expr_factor_op]
	{
		c_expr_factor_op( ExprBitwise.expr_factor_op )
	}
}

void c_expr_mult( ExprMult: expr_mult )
{
	if match ExprMult [expr_mult T: `* expr_bitwise]
	{
		c_expr_mult( ExprMult._expr_mult )
		send CParser [T]
		c_expr_bitwise( ExprMult.expr_bitwise )
	}
	elsif match ExprMult [expr_bitwise]
	{
		c_expr_bitwise( ExprMult.expr_bitwise )
	}
}

void c_expr_add( ExprAdd: expr_add )
{
	if match ExprAdd [expr_add Op: add_op expr_mult]
	{
		c_expr_add( ExprAdd._expr_add )
		send CParser [Op]
		c_expr_mult( ExprAdd.expr_mult )
	}
	elsif match ExprAdd [expr_mult]
	{
		c_expr_mult( ExprAdd.expr_mult )
	}
}

void c_expr_shift( ExprShift: expr_shift )
{
	if match ExprShift [expr_shift Op: shift_op expr_add]
	{
		c_expr_shift( ExprShift._expr_shift )
		send CParser [Op]
		c_expr_add( ExprShift.expr_add )
	}
	elsif match ExprShift [expr_add]
	{
		c_expr_add( ExprShift.expr_add )
	}
}

void c_expr_test( ExprTest: expr_test )
{
	if match ExprTest [expr_test Op: test_op expr_shift]
	{
		c_expr_test( ExprTest._expr_test )
		send CParser [Op]
		c_expr_shift( ExprTest.expr_shift )
	}
	elsif match ExprTest [expr_shift]
	{
		c_expr_shift( ExprTest.expr_shift )
	}
}

void c_expr( Expr: expr )
{
	c_expr_test( Expr.expr_test )
}

void c_type( Type: type )
{
	if match Type "s8"
		send CParser ['char ']
	elsif match Type "s16"
		send CParser ['short ']
	elsif match Type "s32"
		send CParser ['int ']
	elsif match Type "s64"
		send CParser ['long ']
	elsif match Type "s128"
		send CParser ['long long ']
	elsif match Type "uint"
		send CParser ['unsigned int ']
	else
		send CParser [Type]
}

void c_number( Number: number )
{
	if match Number [`u `( uint `) ]
		send CParser "[Number.uint]u"
	else
		send CParser [Number]
}

void c_num_list( NumList: num_list )
{
	for Number: number in NumList
		send CParser "[c_number( Number )], "
}

void c_stmt( Stmt: stmt )
{
	if match Stmt
		[EH: embedded_host]
	{
		send CParser
			[c_embedded_host( EH )]
	}
	elsif match Stmt [Array: array] {
		send CParser
			"static const [c_type(Array.type)] "
				"[Array.ident] \[\] = { [c_num_list(Array.num_list)] };
	}
	elsif match Stmt [Value: value] {
		send CParser
			"static const [Value.type] [Value.ident] = [Value.number];
	}
	elsif match Stmt [
			'if' O: `( IfExpr: expr C: `) IfStmt: stmt
			ElseIfClauseList: else_if_clause* ElseClauseOpt: else_clause?
	] {
		send CParser
			"if ( [c_expr(IfExpr)] )
			"	[c_stmt(IfStmt)]

		for ElseIfClause: else_if_clause in repeat( ElseIfClauseList ) {
			match ElseIfClause
				['else if (' ElseIfExpr: expr ')' ElseIfStmt: stmt]

			send CParser
				"else if ( [c_expr(ElseIfExpr)] )
				"	[c_stmt(ElseIfStmt)]
		}

		if ( match ElseClauseOpt ['else' ElseStmt: stmt] ) {
			send CParser
				"else
				"	[c_stmt(ElseStmt)]
		}
	}
	elsif match Stmt ['while' '(' WhileExpr: expr ')' WhileStmt: stmt] {
		send CParser
			"while ( [c_expr(WhileExpr)] )
			"	[c_stmt(WhileStmt)]
	}
	elsif match Stmt ['switch' '(' SwitchExpr: expr ')' '{' StmtList: stmt* '}'] {
		send CParser
			"switch ( [c_expr(SwitchExpr)] ) {
			"	[c_stmt_list(StmtList)]
			"}
	}
	elsif match Stmt [ExprExpr: expr Semi: `;] {
		send CParser
			[c_expr(ExprExpr) Semi]
	}
	elsif match Stmt [L: `{ TL: stmt* R: `}] {
		send CParser
			[L c_stmt_list(TL) R]
	}
	elsif match Stmt [TypeList: opt_const Type: type OptPtr: opt_ptr Ident: ident OptInit: opt_init Semi: `;]
	{
		send CParser
			[TypeList c_type(Type) OptPtr Ident]

		if match OptInit [E: `= expr] {
			send CParser
				[E c_expr(OptInit.expr)]
		}

		send CParser
			[Semi]
	}
	elsif match Stmt [Export: export_stmt]
	{
		send CParser
			"#define [Export.ident] [c_number(Export.number)] 
	}
	elsif match Stmt ['fallthrough' ';']
	{
		# Nothing needed here.
	}
	elsif match Stmt [Index: index_stmt]
	{
		send CParser
			"const [c_type(Index.type)] *[Index.ident];
	}
	elsif match Stmt [case_block]
	{
		send CParser
			"case [c_expr( Stmt.case_block.expr )]:
			"[c_stmt_list( Stmt.case_block._repeat_stmt )]
			"break;
	}
	elsif match Stmt [default_block]
	{
		send CParser
			"default:
			"[c_stmt_list( Stmt.default_block._repeat_stmt )]
			"break;
	}
	elsif match Stmt [case_label]
	{
		send CParser
			"case [c_expr( Stmt.case_label.expr )]:
	}
	elsif match Stmt [label_stmt]
	{
		send CParser
			"[Stmt.label_stmt.ident]:
			"[c_stmt_list( Stmt.label_stmt._repeat_stmt )]
	}
	elsif match Stmt [entry_loop]
	{
		send CParser
			[c_stmt_list( Stmt.entry_loop._repeat_stmt )]
	}
	elsif match Stmt [AS: assign_stmt]
	{
		send CParser
			"[c_lvalue(AS.LValue) AS.assign_op c_expr(AS.expr)];
	}
	else {
		# catches unspecified cases
		send CParser [Stmt]
	}
}

void c_stmt_list( StmtList: stmt* )
{
	for Stmt: stmt in repeat( StmtList )
		c_stmt( Stmt )
}


void c_trans( StmtList: stmt* )
{
	CParser = new parser<c_out::c_out>()

	c_stmt_list( StmtList )

	CO: c_out::c_out = CParser->finish()

	if CO {
		send Output
			[CO]
	}
	else {
		send stderr
			"failed to parse output: [CParser->error]
	}
}
