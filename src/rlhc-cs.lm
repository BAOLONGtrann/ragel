
namespace cs_out
	token _IN_ /''/
	token _EX_ /''/

	lex
		token comment /
			'//' any* :> '\n' |
			'/*' any* :>> '*/'
		/

		token id
			/[a-zA-Z_][a-zA-Z_0-9]*/

		token number /
			[0-9]+
		/

		token symbol /
			'!' | '#' | '$' | '%' | '&' | '(' | ')' | '*' |
			'+' | ',' | '-' | '.' | '/' | ':' | ';' | '<' |
			'=' | '>' | '?' | '@' | '[' | ']' | '^' | '|' |
			'~' /

		literal `{ `}

		token string /
				'"' ( [^"\\] | '\\' any ) * '"' |
				"'" ( [^'\\] | '\\' any ) * "'"
			/

		ignore
			/[ \t\v\r\n]+/
	end

	def item
		[comment]
	|	[id]
	|	[number]
	|	[symbol]
	|	[string]
	|	[`{ _IN_ item* _EX_ `} ]

	def cs_out
		[_IN_ _EX_ item*]
end

global CsParser: parser<cs_out::cs_out>

void cs_tok_list( TL: host::tok* )
{
	for Tok: host::tok in repeat(TL) {
		if match Tok [host::`${ StmtList: stmt* host::`}$]
			send CsParser "{[cs_stmt_list( StmtList )]}"
		elsif match Tok [host::`={ Expr: expr host::`}=]
			send CsParser "([cs_expr( Expr )])"
		else {
			send CsParser [Tok]
		}
	}
}

void cs_embedded_host( EH: embedded_host )
{
	switch EH
	case [`host `( string `, uint `) `={ TL: host::tok* host::`}=]
	{
		send CsParser
			"([cs_tok_list( TL )])"
	}
	case [`host `( string `, uint `) `${ TL: host::tok* host::`}$]
	{
		send CsParser 
			"{[cs_tok_list( TL )]}
	}
	case [`host `( string `, uint `) `@{ TL: host::tok* host::`}@]
	{
		send CsParser
			[cs_tok_list( TL )]
	}
}

void cs_expr_factor( ExprFactor: expr_factor )
{
	if match ExprFactor
		[EH: embedded_host]
	{
		send CsParser
			[cs_embedded_host( EH )]
	}
	elsif match ExprFactor
		[O:`( TL: expr C: `)]
	{
		send CsParser
			[O cs_expr(TL) C]
	}
	elsif match ExprFactor
		[ident O: `[ TL: expr C: `]]
	{
		send CsParser
			[ExprFactor.ident O cs_expr( TL ) C]
	}
	elsif match ExprFactor
		['offset' '(' ident ',' expr ')']
	{
		send CsParser
			[cs_expr( ExprFactor.expr )]
	}
	elsif match ExprFactor
		['deref' '(' ident ',' expr ')']
	{
		send CsParser
			[ ExprFactor.ident '[' cs_expr( ExprFactor.expr ) ']']
	}
	elsif match ExprFactor
			[T: `TRUE]
	{
		T.data = 'true'
		send CsParser [T]
	}
	elsif match ExprFactor
			[F: `FALSE]
	{
		F.data = 'false'
		send CsParser [F]
	}
	elsif match ExprFactor
			[N: `nil]
	{
		N.data = '0'
		send CsParser [N]
	}
	elsif match ExprFactor
			[Number: number]
	{
		cs_number( Number )
	}
	elsif match ExprFactor [E1: embedded_host `-> E2: expr_factor]
	{
		cs_embedded_host( E1 )
		cs_expr_factor( E2 )
	}
	elsif match ExprFactor [Open: `( Type: type Close: `) expr_factor]
	{
		send CsParser [Open]
		cs_type( Type )
		send CsParser [Close]
		cs_expr_factor( ExprFactor._expr_factor )
	}
	else {
		# Catches cases not specified
		send CsParser [ExprFactor]
	}
}

void cs_lvalue( ExprFactor: lvalue )
{
	if match ExprFactor
		[EH: embedded_host]
	{
		send CsParser
			[cs_embedded_host( EH )]
	}
	elsif match ExprFactor
		[ident O: `[ TL: expr C: `]]
	{
		send CsParser
			[ExprFactor.ident O cs_expr( TL ) C]
	}
	elsif match ExprFactor [E1: embedded_host `-> E2: lvalue]
	{
		cs_embedded_host( E1 )
		cs_lvalue( E2 )
	}
	else {
		# Catches cases not specified
		send CsParser [ExprFactor]
	}
}

void cs_expr_factor_op( ExprFactorOp: expr_factor_op )
{
	if match ExprFactorOp [B: `! expr_factor_op]
	{
		send CsParser [B]
		cs_expr_factor_op( ExprFactorOp._expr_factor_op )
	}
	elsif match ExprFactorOp [T: `~ expr_factor_op]
	{
		send CsParser [T]
		cs_expr_factor_op( ExprFactorOp._expr_factor_op )
	}
	elsif match ExprFactorOp [expr_factor]
	{
		cs_expr_factor( ExprFactorOp.expr_factor )
	}
}

void cs_expr_bitwise( ExprBitwise: expr_bitwise )
{
	if match ExprBitwise [expr_bitwise A: `& expr_factor_op]
	{
		cs_expr_bitwise( ExprBitwise._expr_bitwise )
		send CsParser [A]
		cs_expr_factor_op( ExprBitwise.expr_factor_op )
	}
	elsif match ExprBitwise [expr_factor_op]
	{
		cs_expr_factor_op( ExprBitwise.expr_factor_op )
	}
}

void cs_expr_mult( ExprMult: expr_mult )
{
	if match ExprMult [expr_mult T: `* expr_bitwise]
	{
		cs_expr_mult( ExprMult._expr_mult )
		send CsParser [T]
		cs_expr_bitwise( ExprMult.expr_bitwise )
	}
	elsif match ExprMult [expr_bitwise]
	{
		cs_expr_bitwise( ExprMult.expr_bitwise )
	}
}

void cs_expr_add( ExprAdd: expr_add )
{
	if match ExprAdd [expr_add Op: add_op expr_mult]
	{
		cs_expr_add( ExprAdd._expr_add )
		send CsParser [Op]
		cs_expr_mult( ExprAdd.expr_mult )
	}
	elsif match ExprAdd [expr_mult]
	{
		cs_expr_mult( ExprAdd.expr_mult )
	}
}

void cs_expr_shift( ExprShift: expr_shift )
{
	if match ExprShift [expr_shift Op: shift_op expr_add]
	{
		cs_expr_shift( ExprShift._expr_shift )
		send CsParser [Op]
		cs_expr_add( ExprShift.expr_add )
	}
	elsif match ExprShift [expr_add]
	{
		cs_expr_add( ExprShift.expr_add )
	}
}

void cs_expr_test( ExprTest: expr_test )
{
	if match ExprTest [expr_test Op: test_op expr_shift]
	{
		cs_expr_test( ExprTest._expr_test )
		send CsParser [Op]
		cs_expr_shift( ExprTest.expr_shift )
	}
	elsif match ExprTest [expr_shift]
	{
		cs_expr_shift( ExprTest.expr_shift )
	}
}

void cs_expr( Expr: expr )
{
	cs_expr_test( Expr.expr_test )
}

void cs_type( Type: type )
{
	if match Type "s8"
		send CsParser ['sbyte ']
	elsif match Type "s16"
		send CsParser ['short ']
	elsif match Type "s32"
		send CsParser ['int ']
	elsif match Type "s64"
		send CsParser ['long ']
	elsif match Type "s128"
		send CsParser ['long long ']
	elsif match Type "uint"
		send CsParser ['uint ']
	else
		send CsParser [Type]
}

void cs_number( Number: number )
{
	if match Number [`c `( uint `) ] {
		Str: str = $Number.uint
		send CsParser "'\\u[sprintf( "%04x", Str.atoi() )]'"
	}
	elsif match Number [`u `( uint `) ] {
		send CsParser [$Number.uint]
	}
	else {
		send CsParser [$Number.sint]
	}
}

void cs_num_list( NumList: num_list )
{
	for Number: number in NumList
		send CsParser "[cs_number( Number )], "
}

void cs_stmt( Stmt: stmt )
{
	if match Stmt
		[EH: embedded_host]
	{
		send CsParser
			[cs_embedded_host( EH )]
	}
	elsif match Stmt [Array: array] {
		send CsParser
			"static readonly [cs_type(Array.type)] \[\]"
				"[Array.ident] = { [cs_num_list( Array.num_list )] };
	}
	elsif match Stmt [Value: value] {
		send CsParser
			"static readonly [Value.type] [Value.ident] = [Value.number];
	}
	elsif match Stmt [
			'if' O: `( IfExpr: expr C: `) IfStmt: stmt
			ElseIfClauseList: else_if_clause* ElseClauseOpt: else_clause?
	] {
		send CsParser
			"if ( [cs_expr(IfExpr)] )
			"	[cs_stmt(IfStmt)]

		for ElseIfClause: else_if_clause in repeat( ElseIfClauseList ) {
			match ElseIfClause
				['else if (' ElseIfExpr: expr ')' ElseIfStmt: stmt]

			send CsParser
				"else if ( [cs_expr(ElseIfExpr)] )
				"	[cs_stmt(ElseIfStmt)]
		}

		if ( match ElseClauseOpt ['else' ElseStmt: stmt] ) {
			send CsParser
				"else
				"	[cs_stmt(ElseStmt)]
		}
	}
	elsif match Stmt ['while' '(' WhileExpr: expr ')' WhileStmt: stmt] {
		send CsParser
			"while ( [cs_expr(WhileExpr)] )
			"	[cs_stmt(WhileStmt)]
	}
	elsif match Stmt ['switch' '(' SwitchExpr: expr ')' '{' StmtList: stmt* '}'] {
		send CsParser
			"switch ( [cs_expr(SwitchExpr)] ) {
			"	[cs_stmt_list(StmtList)]
			"}
	}
	elsif match Stmt [ExprExpr: expr Semi: `;] {
		send CsParser
			[cs_expr(ExprExpr) Semi]
	}
	elsif match Stmt [L: `{ TL: stmt* R: `}] {
		send CsParser
			[L cs_stmt_list(TL) R]
	}
	elsif match Stmt [TypeList: opt_const Type: type OptPtr: opt_ptr Ident: ident OptInit: opt_init Semi: `;]
	{
		send CsParser
			[TypeList cs_type(Type) OptPtr Ident]

		if match OptInit [E: `= expr] {
			send CsParser
				[E cs_expr(OptInit.expr)]
		}

		send CsParser
			[Semi]
	}
	elsif match Stmt [Export: export_stmt]
	{
		send CsParser
			"#define [Export.ident] [Export.number] 
	}
	elsif match Stmt ['fallthrough' ';']
	{
		# Nothing needed here.
	}
	elsif match Stmt [index_stmt]
	{
		send CsParser
			"int [Stmt.index_stmt.ident];
	}
	elsif match Stmt [case_block]
	{
		send CsParser
			"case [cs_expr( Stmt.case_block.expr )]:
			"[cs_stmt_list( Stmt.case_block._repeat_stmt )]
			"break;
	}
	elsif match Stmt [default_block]
	{
		send CsParser
			"default:
			"[cs_stmt_list( Stmt.default_block._repeat_stmt )]
			"break;
	}
	elsif match Stmt [case_label]
	{
		send CsParser
			"case [cs_expr( Stmt.case_label.expr )]:
	}
	elsif match Stmt [label_stmt]
	{
		send CsParser
			"[Stmt.label_stmt.ident]:
			"[cs_stmt_list( Stmt.label_stmt._repeat_stmt )]
	}
	elsif match Stmt [entry_loop]
	{
		send CsParser
			[cs_stmt_list( Stmt.entry_loop._repeat_stmt )]
	}
	elsif match Stmt [AS: assign_stmt]
	{
		send CsParser
			"[cs_lvalue(AS.LValue) AS.assign_op cs_expr(AS.expr)];
	}
	else {
		# catches unspecified cases
		send CsParser [Stmt]
	}
}

void cs_stmt_list( StmtList: stmt* )
{
	for Stmt: stmt in repeat( StmtList )
		cs_stmt( Stmt )
}

void cs_trans( StmtList: stmt* )
{
	CsParser = new parser<cs_out::cs_out>()

	cs_stmt_list( StmtList )

	CSO: cs_out::cs_out = CsParser->finish()

	if CSO {
		send Output
			[CSO]
	}
	else {
		send stderr
			"failed to parse output: [CsParser->error]
	}
}
