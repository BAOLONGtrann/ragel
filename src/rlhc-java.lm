void java_tok_list( TL: host::tok* )
{
	for Tok: host::tok in repeat(TL) {
		if match Tok [host::`${ StmtList: stmt* host::`}$]
			send Output "{[java_stmt_list( StmtList )]}"
		elsif match Tok [host::`={ Expr: expr host::`}=]
			send Output "([java_expr( Expr )])"
		else {
			send Output [Tok]
		}
	}
}

void java_expr_factor( ExprFactor: expr_factor )
{
	if match ExprFactor
		[`host `( string `, uint `) `={ TL: host::tok* host::`}=]
	{
		send Output
			"([java_tok_list( TL )])"
	}
	elsif match ExprFactor
		[`host `( string `, uint `) `${ TL: host::tok* host::`}$]
	{
		send Output 
			"{[java_tok_list( TL )]}
	}
	elsif match ExprFactor
		[`host `( string `, uint `) `@{ TL: host::tok* host::`}@]
	{
		send Output
			[java_tok_list( TL )]
	}
	elsif match ExprFactor
		[O:`( TL: expr C: `)]
	{
		send Output
			[O java_expr(TL) C]
	}
	elsif match ExprFactor
		[ident O: `[ TL: expr C: `]]
	{
		send Output
			[ExprFactor.ident O java_expr( TL ) C]
	}
	elsif match ExprFactor
		['offset' '(' ident ',' expr ')']
	{
		send Output
			[java_expr( ExprFactor.expr )]
	}
	elsif match ExprFactor
		['deref' '(' ident ',' expr ')']
	{
		send Output
			[ ExprFactor.ident '[' java_expr( ExprFactor.expr ) ']']
	}
	elsif match ExprFactor
			[T: `TRUE]
	{
		T.data = 'true'
		send Output [T]
	}
	elsif match ExprFactor
			[F: `FALSE]
	{
		F.data = 'false'
		send Output [F]
	}
	elsif match ExprFactor
			[N: `nil]
	{
		N.data = '0'
		send Output [N]
	}
	elsif match ExprFactor
			[Number: number]
	{
		java_number( Number )
	}
	else {
		# Catches cases not specified
		send Output [ExprFactor]
	}
}

void java_expr_ptr( ExprPtr: expr_ptr )
{
	if match ExprPtr [expr_ptr Arrow: `-> expr_factor]
	{
		java_expr_ptr( ExprPtr._expr_ptr )
		java_expr_factor( ExprPtr.expr_factor )
	}
	elsif match ExprPtr [Open: `( Type: type Close: `) expr_ptr]
	{
		send Output [Open]
		java_type( Type )
		send Output [Close]
		java_expr_ptr( ExprPtr._expr_ptr )
	}
	elsif match ExprPtr [expr_factor]
	{
		java_expr_factor( ExprPtr.expr_factor )
	}
	else {
		send Output [ExprPtr]
	}
}

void java_expr_factor_op( ExprFactorOp: expr_factor_op )
{
	if match ExprFactorOp [B: `! expr_factor_op]
	{
		send Output [B]
		java_expr_factor_op( ExprFactorOp._expr_factor_op )
	}
	elsif match ExprFactorOp [T: `~ expr_factor_op]
	{
		send Output [T]
		java_expr_factor_op( ExprFactorOp._expr_factor_op )
	}
	elsif match ExprFactorOp [expr_ptr]
	{
		java_expr_ptr( ExprFactorOp.expr_ptr )
	}
}

void java_expr_bitwise( ExprBitwise: expr_bitwise )
{
	if match ExprBitwise [expr_bitwise A: `& expr_factor_op]
	{
		java_expr_bitwise( ExprBitwise._expr_bitwise )
		send Output [A]
		java_expr_factor_op( ExprBitwise.expr_factor_op )
	}
	elsif match ExprBitwise [expr_factor_op]
	{
		java_expr_factor_op( ExprBitwise.expr_factor_op )
	}
}

void java_expr_mult( ExprMult: expr_mult )
{
	if match ExprMult [expr_mult T: `* expr_bitwise]
	{
		java_expr_mult( ExprMult._expr_mult )
		send Output [T]
		java_expr_bitwise( ExprMult.expr_bitwise )
	}
	elsif match ExprMult [expr_bitwise]
	{
		java_expr_bitwise( ExprMult.expr_bitwise )
	}
}

void java_expr_add( ExprAdd: expr_add )
{
	if match ExprAdd [expr_add Op: add_op expr_mult]
	{
		java_expr_add( ExprAdd._expr_add )
		send Output [Op]
		java_expr_mult( ExprAdd.expr_mult )
	}
	elsif match ExprAdd [expr_mult]
	{
		java_expr_mult( ExprAdd.expr_mult )
	}
}

void java_expr_shift( ExprShift: expr_shift )
{
	if match ExprShift [expr_shift Op: shift_op expr_add]
	{
		java_expr_shift( ExprShift._expr_shift )
		send Output [Op]
		java_expr_add( ExprShift.expr_add )
	}
	elsif match ExprShift [expr_add]
	{
		java_expr_add( ExprShift.expr_add )
	}
}

void java_expr_test( ExprTest: expr_test )
{
	if match ExprTest [expr_test Op: test_op expr_shift]
	{
		java_expr_test( ExprTest._expr_test )
		send Output [Op]
		java_expr_shift( ExprTest.expr_shift )
	}
	elsif match ExprTest [expr_shift]
	{
		java_expr_shift( ExprTest.expr_shift )
	}
}

void java_expr_assign( ExprAssign: expr_assign )
{
	if match ExprAssign [expr_assign Op: assign_op expr_test]
	{
		java_expr_assign( ExprAssign._expr_assign )
		send Output [Op]
		java_expr_test( ExprAssign.expr_test )
	}
	elsif match ExprAssign [expr_test]
	{
		java_expr_test( ExprAssign.expr_test )
	}
}

void java_expr( Expr: expr )
{
	java_expr_assign( Expr.expr_assign )
}

void java_type( Type: type )
{
	if match Type "s8"
		send Output ['byte ']
	elsif match Type "s16"
		send Output ['short ']
	elsif match Type "s32"
		send Output ['int ']
	elsif match Type "s64"
		send Output ['long ']
	elsif match Type "s128"
		send Output ['long long ']
	elsif match Type "uint"
		send Output ['int ']
	else
		send Output [Type]
}

void java_number( Number: number )
{
	if match Number [`u `( uint `) ]
		send Output "[Number.uint]"
	else
		send Output [Number]
}

void java_num_list( NumList: num_list )
{
	for Number: number in NumList
		send Output "[java_number( Number )], "
}

void java_stmt( Stmt: stmt )
{
	if match Stmt
		[`host `( string `, uint `) `={ TL: host::tok* host::`}=]
	{
		send Output
			"([java_tok_list( TL )])"
	}
	elsif match Stmt
		[`host `( string `, uint `) `${ TL: host::tok* host::`}$]
	{
		send Output 
			"{[java_tok_list( TL )]}
	}
	elsif match Stmt
		[`host `( string `, uint `) `@{ TL: host::tok* host::`}@]
	{
		send Output
			[java_tok_list( TL )]
	}
	elsif match Stmt [Array: array] {
		send Output
			"private static [java_type(Array.type)] "
				"[Array.ident] \[\] = { [java_num_list(Array.num_list)] };
	}
	elsif match Stmt [Value: value] {
		send Output
			"private static [Value.type] [Value.ident] = [Value.number];
	}
	elsif match Stmt [
			'if' O: `( IfExpr: expr C: `) IfStmt: stmt
			ElseIfClauseList: else_if_clause* ElseClauseOpt: else_clause?
	] {
		send Output
			"if ( [java_expr(IfExpr)] )
			"	[java_stmt(IfStmt)]

		for ElseIfClause: else_if_clause in repeat( ElseIfClauseList ) {
			match ElseIfClause
				['else if (' ElseIfExpr: expr ')' ElseIfStmt: stmt]

			send Output
				"else if ( [java_expr(ElseIfExpr)] )
				"	[java_stmt(ElseIfStmt)]
		}

		if ( match ElseClauseOpt ['else' ElseStmt: stmt] ) {
			send Output
				"else
				"	[java_stmt(ElseStmt)]
		}
	}
	elsif match Stmt ['while' '(' WhileExpr: expr ')' WhileStmt: stmt] {
		send Output
			"while ( [java_expr(WhileExpr)] )
			"	[java_stmt(WhileStmt)]
	}
	elsif match Stmt ['switch' '(' SwitchExpr: expr ')' '{' StmtList: stmt* '}'] {
		send Output
			"switch ( [java_expr(SwitchExpr)] ) {
			"	[java_stmt_list(StmtList)]
			"}
	}
	elsif match Stmt [ExprExpr: expr Semi: `;] {
		send Output
			[java_expr(ExprExpr) Semi]
	}
	elsif match Stmt [L: `{ TL: stmt* R: `}] {
		send Output
			[L java_stmt_list(TL) R]
	}
	elsif match Stmt [TypeList: opt_const Type: type OptPtr: opt_ptr Ident: ident OptInit: opt_init Semi: `;]
	{
		send Output
			[TypeList java_type(Type) OptPtr Ident]

		if match OptInit [E: `= expr] {
			send Output
				[E java_expr(OptInit.expr)]
		}

		send Output
			[Semi]
	}
	elsif match Stmt [Export: export_stmt]
	{
		send Output
			"static final char [Export.ident] = [java_number(Export.number)];
	}
	elsif match Stmt ['fallthrough' ';']
	{
		# Nothing needed here.
	}
	elsif match Stmt [Index: index_stmt]
	{
		send Output
			"int [Index.ident];
	}
	elsif match Stmt [case_block]
	{
		send Output
			"case [java_expr( Stmt.case_block.expr )]:
			"[java_stmt_list( Stmt.case_block._repeat_stmt )]
			"break;
	}
	elsif match Stmt [default_block]
	{
		send Output
			"default:
			"[java_stmt_list( Stmt.default_block._repeat_stmt )]
			"break;
	}
	elsif match Stmt [goto_label]
	{
		send Output "{}"
	}
	elsif match Stmt [G: goto_stmt]
	{
		send Output
			"{ _goto_targ = [$G.Id]; if (true) continue _goto; }
	}
	elsif match Stmt [entry_loop]
	{
		send Output
			"int _goto_targ = 0;
			"_goto: while ( true ) {
			"	switch ( _goto_targ ) {
			"	case 0:
			"	[java_stmt_list( Stmt.entry_loop._repeat_stmt )]
			"	}
			"	break;
			"}
	}
	elsif match Stmt [label_stmt]
	{
		send Output
			"case [Stmt.label_stmt.Id]:
			"[java_stmt_list( Stmt.label_stmt._repeat_stmt )]
	}
	else {
		# catches unspecified cases
		send Output [Stmt]
	}
}

void java_stmt_list( StmtList: stmt* )
{
	for Stmt: stmt in repeat( StmtList )
		java_stmt( Stmt )
}
