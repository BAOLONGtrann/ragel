global StaticVarMap: map<str str> = new map<str str>

void ml_tok_list( TL: host::tok* )
{
	for Tok: host::tok in repeat(TL) {
		if match Tok [host::`${ StmtList: stmt* host::`}$]
			send Output
				"begin
				"[ml_stmt_list( StmtList )]
				"end
		elsif match Tok [host::`={ Expr: expr host::`}=]
			send Output "([ml_expr( Expr )])"
		else {
			send Output [Tok]
		}
	}
}

void ml_embedded_host( EH: embedded_host )
{
	switch EH
	case [`host `( string `, uint `) `={ TL: host::tok* host::`}=]
	{
		send Output
			"([ml_tok_list( TL )])"
	}
	case [`host `( string `, uint `) `${ TL: host::tok* host::`}$]
	{
		send Output 
			"begin
			"[ml_tok_list( TL )]
			"end;
	}
	case [`host `( string `, uint `) `@{ TL: host::tok* host::`}@]
	{
		send Output
			[ml_tok_list( TL )]
	}
}

void ml_expr_factor( ExprFactor: expr_factor )
{
	switch ExprFactor
	case [EH: embedded_host]
	{
		send Output
			[ml_embedded_host( EH )]
	}
	case [`( TL: expr `)]
	{
		send Output
			"( [ ml_expr(TL) ] )"
	}
	case [I: ident `[ TL: expr `]]
	{
		if ( StaticVarMap->find( $I ) || $I == 'stack' ) {
			send Output
				"[ ExprFactor.ident ].([ ml_expr( TL ) ])"
		}
		else {
			send Output
				"[ ExprFactor.ident ].\[[ ml_expr( TL ) ]\]"
		}
	}
	case ['offset' '(' ident ',' expr ')']
	{
		send Output
			[ml_expr( ExprFactor.expr )]
	}
	case ['deref' '(' I: ident ',' Expr: expr ')']
	{
		if ( $I == 'data' )
			send Output 'Char.code '

		if ( StaticVarMap->find( $I ) ) {
			send Output
				"[I].( [ ml_expr( Expr ) ] )"
		}
		else {
			send Output
				"[I].\[[ ml_expr( Expr ) ]\]"
		}
	}
	case [T: `TRUE]
	{
		T.data = '1'
		send Output [T]
	}
	case [F: `FALSE]
	{
		F.data = '0'
		send Output [F]
	}
	case [N: `nil]
	{
		N.data = '0'
		send Output [N]
	}
	case [Number: number]
	{
		ml_number( Number )
	}
	case [I: ident] {
		if ( StaticVarMap->find( $I ) ) {
			send Output
				[^I]
		}
		else {
			send Output
				"[^I].contents"
		}
	}
	case [E1: embedded_host `-> E2: expr_factor]
	{
		# The accessor operator is contained wihtin the lhs.
		ml_embedded_host( E1 )
		ml_expr_factor( E2 )
	}
	case [`( type `) expr_factor]
	{
		send Output
			[ml_expr_factor( ExprFactor._expr_factor )]
	}
	default {
		# Catches cases not specified
		send Output [ExprFactor]
	}
}

void ml_lvalue( ExprFactor: lvalue )
{
	switch ExprFactor
	case [EH: embedded_host]
	{
		send Output
			[ml_embedded_host( EH )]
	}
	case [ident O: `[ TL: expr C: `]]
	{
		send Output
			[ExprFactor.ident O ml_expr( TL ) C]
	}
	case [E1: embedded_host `-> E2: lvalue]
	{
		# The accessor operator is contained wihtin the lhs.
		ml_embedded_host( E1 )
		ml_lvalue( E2 )
	}
	default {
		# Catches cases not specified
		send Output [ExprFactor]
	}
}

void ml_expr_factor_op( ExprFactorOp: expr_factor_op )
{
	if match ExprFactorOp [B: `! expr_factor_op]
	{
		send Output [B]
		ml_expr_factor_op( ExprFactorOp._expr_factor_op )
	}
	elsif match ExprFactorOp [T: `~ expr_factor_op]
	{
		send Output " lnot "
		ml_expr_factor_op( ExprFactorOp._expr_factor_op )
	}
	elsif match ExprFactorOp [expr_factor]
	{
		ml_expr_factor( ExprFactorOp.expr_factor )
	}
}

void ml_expr_bitwise( ExprBitwise: expr_bitwise )
{
	if match ExprBitwise [L: expr_bitwise `& R: expr_factor_op]
	{
		send Output
			"[ml_expr_bitwise( L )] land [ml_expr_factor_op( R )]"
	}
	elsif match ExprBitwise [expr_factor_op]
	{
		ml_expr_factor_op( ExprBitwise.expr_factor_op )
	}
}

void ml_expr_mult( ExprMult: expr_mult )
{
	if match ExprMult [expr_mult T: `* expr_bitwise]
	{
		ml_expr_mult( ExprMult._expr_mult )
		send Output [T]
		ml_expr_bitwise( ExprMult.expr_bitwise )
	}
	elsif match ExprMult [expr_bitwise]
	{
		ml_expr_bitwise( ExprMult.expr_bitwise )
	}
}

void ml_expr_add( ExprAdd: expr_add )
{
	if match ExprAdd [expr_add Op: add_op expr_mult]
	{
		ml_expr_add( ExprAdd._expr_add )
		send Output [Op]
		ml_expr_mult( ExprAdd.expr_mult )
	}
	elsif match ExprAdd [expr_mult]
	{
		ml_expr_mult( ExprAdd.expr_mult )
	}
}

void ml_expr_shift( ExprShift: expr_shift )
{
	if match ExprShift [expr_shift Op: shift_op expr_add]
	{
		ml_expr_shift( ExprShift._expr_shift )
		if match Op [`<<]
			send Output " lsl "
		else
			send Output " asr "
		ml_expr_add( ExprShift.expr_add )
	}
	elsif match ExprShift [expr_add]
	{
		ml_expr_add( ExprShift.expr_add )
	}
}

void ml_expr_test( ExprTest: expr_test )
{
	if match ExprTest [expr_test Op: test_op expr_shift]
	{
		ml_expr_test( ExprTest._expr_test )
		if match Op [`==]
			send Output "= "
		else
			send Output [Op]
		ml_expr_shift( ExprTest.expr_shift )
	}
	elsif match ExprTest [expr_shift]
	{
		ml_expr_shift( ExprTest.expr_shift )
	}
}

void ml_expr( Expr: expr )
{
	ml_expr_test( Expr.expr_test )
}

void ml_type( Type: type )
{
	if match Type "s8"
		send Output ['char ']
	elsif match Type "s16"
		send Output ['short ']
	elsif match Type "s32"
		send Output ['int ']
	elsif match Type "s64"
		send Output ['long ']
	elsif match Type "s128"
		send Output ['long long ']
	elsif match Type "uint"
		send Output ['int ']
	else
		send Output [Type]
}

void ml_number( Number: number )
{
	if match Number [`u `( uint `) ]
		send Output "[Number.uint]u"
	else
		send Output [Number]
}

void ml_num_list( NumList: num_list )
{
	for Number: number in NumList
		send Output "[ml_number( Number )]; "
}

void ml_stmt( Stmt: stmt )
{
	if match Stmt
		[EH: embedded_host]
	{
		send Output
			[ml_embedded_host( EH )]
	}
	elsif match Stmt [Array: array] {
		StaticVarMap->insert( $Array.ident ' ' )
		send Output
			"let [Array.ident] : int array = \[|
				"	[ml_num_list(Array.num_list)]
			"|\]
	}
	elsif match Stmt [Value: value] {
		StaticVarMap->insert( $Value.ident ' ' )
		send Output
			"let [Value.ident] : [Value.type] = [Value.number]
	}
	elsif match Stmt [
			'if' O: `( IfExpr: expr C: `) IfStmt: stmt
			ElseIfClauseList: else_if_clause* ElseClauseOpt: else_clause?
	] {
		send Output
			"if [ml_expr(IfExpr)] then
			"begin
			"	[ml_stmt(IfStmt)]
			"end

		for ElseIfClause: else_if_clause in repeat( ElseIfClauseList ) {
			match ElseIfClause
				['else if (' ElseIfExpr: expr ')' ElseIfStmt: stmt]

			send Output
				"else if [ml_expr(ElseIfExpr)] then
				"begin
				"	[ml_stmt(ElseIfStmt)]
				"end
		}

		if ( match ElseClauseOpt ['else' ElseStmt: stmt] ) {
			send Output
				"else
				"begin
				"	[ml_stmt(ElseStmt)]
				"end
		}
		send Output ";"
	}
	elsif match Stmt ['while' '(' WhileExpr: expr ')' WhileStmt: stmt] {
		send Output
			"while [ml_expr(WhileExpr)] do
			"	[ml_stmt(WhileStmt)]
			"done;
	}
	elsif match Stmt ['switch' '(' SwitchExpr: expr ')' '{' StmtList: stmt* '}'] {
		require StmtList
			[`case E1: expr `{ Inner: stmt* `} Rest: stmt*]

		send Output
			"if [ml_expr(SwitchExpr)] = [ml_expr(E1)] then
			"begin
			"	[ml_stmt_list(Inner)]
			"end

		for S: stmt in repeat(Rest) {
			if match S
				[`case E1: expr `{ Inner: stmt* `}]
			{
				send Output
					"else if [ml_expr(SwitchExpr)] = [ml_expr(E1)] then
					"begin
					"	[ml_stmt_list(Inner)]
					"end
			}
			elsif match S
				[`default `{ Inner: stmt* `}]
			{
				send Output
					"else
					"begin
					"	[ml_stmt_list(Inner)]
					"end
			}
		}

		send Output
			";
	}
	elsif match Stmt [ExprExpr: expr Semi: `;] {
		send Output
			[ml_expr(ExprExpr) Semi]
	}
	elsif match Stmt [L: `{ TL: stmt* R: `}] {
		send Output
			"begin
			"[ml_stmt_list(TL)]
			"end;
	}
	elsif match Stmt [D: declaration] {
		send Output
			"let [D.ident] : [ml_type(D.type)] ref "

		if match D.opt_init [E: `= expr] {
			send Output
				"= ref [ml_expr(D.opt_init.expr)]"
		}
		else {
			send Output
				"= ref 0"
		}

		send Output
			" in
	}
	elsif match Stmt [Export: export_stmt]
	{
		send Output
			"#define [Export.ident] [ml_number(Export.number)] 
	}
	elsif match Stmt ['fallthrough' ';']
	{
		# Nothing needed here.
	}
	elsif match Stmt [Index: index_stmt]
	{
		send Output
			"let [Index.ident] : int ref = ref 0 in
	}
	elsif match Stmt [case_block]
	{
		send Output
			"| [ml_expr( Stmt.case_block.expr )] -> 
			"begin
			"[ml_stmt_list( Stmt.case_block._repeat_stmt )]
			"end;
	}
	elsif match Stmt [default_block]
	{
		send Output
			"| _ ->
			"[ml_stmt_list( Stmt.default_block._repeat_stmt )]
	}
	elsif match Stmt [case_label]
	{
		send Output
			"case [ml_expr( Stmt.case_label.expr )]:
	}
	elsif match Stmt [label_stmt]
	{
		send Output
			"[Stmt.label_stmt.ident]:
			"[ml_stmt_list( Stmt.label_stmt._repeat_stmt )]
	}
	elsif match Stmt [entry_loop]
	{
		send Output
			[ml_stmt_list( Stmt.entry_loop._repeat_stmt )]
	}
	elsif match Stmt [AS: assign_stmt]
	{
		switch AS.assign_op 
		case [`=]
		{
			if match AS.LValue "stack\[[expr]\]" {
				send Output "Array.set stack top.contents [ml_expr(AS.expr)];"
			}
			else {
				send Output
					"[ml_lvalue(AS.LValue)] := [ml_expr(AS.expr)];
			}
		}
		case [`+=]
		{
			parse RhsAsFactor: expr_factor [$AS.LValue]
			send Output
				"[ml_lvalue(AS.LValue)] := [ml_expr_factor(RhsAsFactor)] + [ml_expr(AS.expr)];
		}
		case [`-=]
		{
			parse RhsAsFactor: expr_factor [$AS.LValue]
			send Output
				"[ml_lvalue(AS.LValue)] := [ml_expr_factor(RhsAsFactor)] - [ml_expr(AS.expr)];
		}
		default {
			send Output
				"[ml_lvalue(AS.LValue) AS.assign_op ml_expr(AS.expr)];
		}
	}
	else {
		# catches unspecified cases
		send Output [Stmt]
	}
}

void ml_stmt_list( StmtList: stmt* )
{
	for Stmt: stmt in repeat( StmtList )
		ml_stmt( Stmt )
}
